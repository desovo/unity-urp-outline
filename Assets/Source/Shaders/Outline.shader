// References:
// https://docs.unity3d.com/Packages/com.unity.render-pipelines.universal@14.0/manual/renderer-features/how-to-fullscreen-blit.html

Shader "Custom/Outline"
{
    Properties
    {
        [HDR] _OutlineColor("Outline Color", Color) = (1, 1, 1, 1)
        _OutlineWidth("Outline Width", Range(0.0, 0.01)) = 0.004
    }
    
    SubShader
    {
        Tags
        {
            "RenderType" = "Opaque"
            "RenderPipeline" = "UniversalPipeline"
        }
        
        Cull Off
        ZWrite Off
        Blend SrcAlpha OneMinusSrcAlpha
        
        Pass
        {
            HLSLPROGRAM
            
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"

            #pragma vertex vert
            #pragma fragment frag

            struct  Attribute
            {
                uint vertexID : SV_VertexID;
            };

            struct Varying
            {
                float4 positionCS : SV_POSITION;
                float2 uv : TEXCOORD0;
                half2 offsets[8] : TEXCOORD1;
            };

            TEXTURE2D_X(_OutlineMask);
            TEXTURE2D_X_FLOAT(_OutlineMaskDepth);
            TEXTURE2D_X_FLOAT(_CameraDepthTexture);
            // https://docs.unity3d.com/Manual/SL-SamplerStates.html
            SAMPLER(sampler_linear_clamp_OutlineMask);
            SAMPLER(sampler_point_clamp);

            half4 _OutlineColor;
            half _OutlineWidth;

            Varying vert(Attribute IN)
            {
                Varying OUT;

                // 使用方法库函数生成全屏三角形顶点位置和UV坐标。
                OUT.positionCS = GetFullScreenTriangleVertexPosition(IN.vertexID);
                OUT.uv = GetFullScreenTriangleTexCoord(IN.vertexID);

                // 计算屏幕宽高比，确保偏移量在不同分辨率下一致。
                const half correction = _ScreenParams.x / _ScreenParams.y;

                // 计算采样偏移量，形成一个环绕当前像素的采样点阵列。
                // 0.707是1/sqrt(2)，用于对角线方向的缩放。最终所有方向的偏移量长度都为_OutlineWidth。
                OUT.offsets[0] = half2(-1, correction) * 0.707 * _OutlineWidth; // Top-left
                OUT.offsets[1] = half2(0, correction) * _OutlineWidth;  // Top
                OUT.offsets[2] = half2(1, correction) * 0.707 * _OutlineWidth;  // Top-right
                OUT.offsets[3] = half2(-1, 0) * _OutlineWidth; // Left
                OUT.offsets[4] = half2(1, 0) * _OutlineWidth;  // Right
                OUT.offsets[5] = half2(-1, -correction) * 0.707 * _OutlineWidth; // Bottom-left
                OUT.offsets[6] = half2(0, -correction) * _OutlineWidth;  // Bottom
                OUT.offsets[7] = half2(1, -correction) * 0.707 * _OutlineWidth;  // Bottom-right

                return OUT;
            }

            half4 frag(Varying IN) : SV_Target
            {
                // 读取场景深度（当前像素的实际深度）。
                float sceneDepth = SAMPLE_TEXTURE2D_X(_CameraDepthTexture, sampler_point_clamp, IN.uv).r;
                // 读取轮廓对象的深度。
                float maskDepth = SAMPLE_TEXTURE2D_X(_OutlineMaskDepth, sampler_point_clamp, IN.uv).r;
                
                // 如果场景深度更近（深度值更小），说明有物体遮挡，不绘制轮廓。
                // 使用一个小的阈值来避免浮点误差。
                if (sceneDepth < maskDepth - 0.0001)
                {
                    discard;
                }
                
                // Sobel 算子核。
                const half kernel_y[8] = {
                    -1, -2, -1,
                     0,      0,
                     1,  2,  1,
                };
                const half kernel_x[8] = {
                    -1,  0,  1,
                    -2,      2,
                    -1,  0,  1,
                };

                // 使用 Sobel 算子计算边缘强度。
                half gx = 0; half gy = 0;
                for (int i = 0; i < 8; i++)
                {
                    half mask = SAMPLE_TEXTURE2D_X(_OutlineMask, sampler_linear_clamp_OutlineMask, IN.uv + IN.offsets[i]).r;
                    gx += mask * kernel_x[i];
                    gy += mask * kernel_y[i];
                }

                // 读取原始遮罩的 alpha 通道，以确保描边不会覆盖物体本身。
                const half alpha = SAMPLE_TEXTURE2D_X(_OutlineMask, sampler_linear_clamp_OutlineMask, IN.uv).a;
                
                half4 col = _OutlineColor;
                // 确保描边不会覆盖物体本身。当物体透明时，向内绘制一些描边。
                col.a = saturate(abs(gx) + abs(gy)) * saturate(1.0 - alpha - 0.5);
                
                return col;
            }
            
            ENDHLSL
        }
    }
}